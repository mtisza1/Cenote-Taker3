import os

## set up temporary directory
if not os.path.isdir(os.path.join(config["run_title"], "ct_tmp")):
    os.makedirs(os.path.join(config["run_title"], "ct_tmp"))

TEMP_DIR = os.path.join(config["run_title"], "ct_tmp")

### setting universal minimum length
if config["circ_length_cutoff"] > config["linear_length_cutoff"]:
    LENGTH_MINIMUM = config["linear_length_cutoff"]
else:
    LENGTH_MINIMUM = config["circ_length_cutoff"]

if config["CIRC_MINIMUM_DOMAINS"] >= config["LIN_MINIMUM_DOMAINS"]:
	HALLMARK_MINIMUM = config["LIN_MINIMUM_DOMAINS"]
else:
	HALLMARK_MINIMUM = config["CIRC_MINIMUM_DOMAINS"]


## more variables from config
run_title = config["run_title"]
CPU = config["CPU"]


def find_files(directory, prefix, ext):
    split_list = []
    for split in os.listdir(directory):
        if split.startswith(prefix) and split.endswith(ext):
            f = os.path.join(directory, split)

            if os.path.isfile(f) and os.path.getsize(f) > 0:
                split_list.append(f)
    try:
        return split_list
    except:
        return ""

def seqkit_splits(splits):
    n=range(1,splits+1)
    n000 = [f"{i:03d}" for i in n]
    return n000

def get_split_outputs(wildcards):
    # this makes the rest of the workflow wait for the checkpoint
    chk_out = checkpoints.seq_split1.get(**wildcards).output[0]
    SMP, = glob_wildcards(os.path.join(chk_out, "{sample}.fasta"))
    return expand(os.path.join(chk_out, "{SAMPLE}.fasta"), SAMPLE=SMP)

spl_og_dir = os.path.join(TEMP_DIR, "split_orig_contigs")
spl_prod_dir = os.path.join(TEMP_DIR, "split_og_prod")

wildcard_constraints:
    seqn = "\d+"

###############
#### rules ####
###############
rule all:
    input:
        assess_tab = os.path.join(TEMP_DIR, "assess_prune/contig_gene_annotation_summary.tsv"),
        hm_bed = os.path.join(TEMP_DIR, "assess_prune/contig_gene_annotation_summary.hallmarks.bed"),
        indiv_dir = directory(os.path.join(TEMP_DIR, "assess_prune/indiv_seqs"))


rule filter_contig_length:
    input:
        og_contigs = config["original_contigs"]
    params:
        lmin = LENGTH_MINIMUM,
        run = run_title
    output:
        filt_contigs = os.path.join(run_title, \
            f"{run_title}.contigs_over_{LENGTH_MINIMUM}nt.fasta")
    shell:
        '''
        seqkit seq --quiet -m {params.lmin} {input.og_contigs} |\
            seqkit replace --quiet -p '^' -r {params.run}_{{nr}}@#@# |\
            sed 's/@#@#/ /g' > {output.filt_contigs}
        '''



checkpoint seq_split1:
    input:
        filt_contigs = os.path.join(run_title, \
            f"{run_title}.contigs_over_{LENGTH_MINIMUM}nt.fasta")
    params:
        CPU = config["CPU"],
        dir = os.path.join(TEMP_DIR, "split_orig_contigs")
    output:
        #split_fna = expand(os.path.join(spl_og_dir, run_title +\
        #    ".contigs_over_" + str(LENGTH_MINIMUM) + "nt.part_{seqn}.fasta"), \
        #    seqn=seqkit_splits(CPU), allow_missing=True),
        #directory(spl_og_dir)
        split_fna = os.path.join(spl_og_dir, f"{run_title}.contigs_over_{LENGTH_MINIMUM}nt.part_" + "{seqn}.fasta")
    shell:
        '''
        seqkit split --quiet -j {params.CPU} -p {params.CPU} \
          -O {params.dir} {input.filt_contigs}
        
        touch {output.split_fna}
        '''


rule prodigal_og:
    input:
        #split_fna = [os.path.join(spl_og_dir, f"{run_title}.contigs_over_{LENGTH_MINIMUM}nt.part_{seqn}.fasta") for seqn in seqkit_splits(CPU)]
        split_fna = os.path.join(spl_og_dir, f"{run_title}.contigs_over_{LENGTH_MINIMUM}nt.part_" + "{seqn}.fasta")
    output:
        #split_faa = [os.path.join(spl_og_dir, f"{run_title}.contigs_over_{LENGTH_MINIMUM}nt.part_{seqn}.prod.faa") for seqn in seqkit_splits(CPU)]
        split_faa = os.path.join(spl_og_dir, f"{run_title}.contigs_over_{LENGTH_MINIMUM}nt.part_" + "{seqn}.prod.faa")
    log:
       #[os.path.join(spl_og_dir, f"{run_title}.contigs_over_{LENGTH_MINIMUM}nt.part_{seqn}.prod.log") for seqn in seqkit_splits(CPU)]
       os.path.join(spl_og_dir, f"{run_title}.contigs_over_{LENGTH_MINIMUM}nt.part_" + "{seqn}.prod.log")
    shell:
        '''
        if [ -s {input.split_fna} ] ; then
            prodigal -a {output.split_faa} -i {input.split_fna} -p meta -q > {log} 2>&1
        else
            touch {output.split_faa}
        fi
        '''


rule pyhmmer_prodigal_og_vir:
    input:
        split_faa = [os.path.join(spl_og_dir, f"{run_title}.contigs_over_{LENGTH_MINIMUM}nt.part_{seqn}.prod.faa") for seqn in seqkit_splits(CPU)]
    params:
        script_dir = config['CENOTE_SCRIPTS'],
        CPU = config["CPU"],
        indir = spl_og_dir,
        virdir = os.path.join(TEMP_DIR, "orig_pyhmmer_virion"),
        viriondb = os.path.join(config['C_DBS'], "hmmscan_DBs/virus_specific_baits_plus_missed6a.h3m"),
        evalue = "1e-8"
    output:
        vir_contig_hits = os.path.join(TEMP_DIR, "orig_pyhmmer_virion/contig_hit_count.tsv"),
        vir_report = os.path.join(TEMP_DIR, "orig_pyhmmer_virion/pyhmmer_report_AAs.tsv"),
    shell:
        '''
        python {params.script_dir}/python_modules/pyhmmer_runner.py {params.indir} {params.virdir}\
          {params.viriondb} {params.CPU} {params.evalue}
        '''

rule pyhmmer_prodigal_og_rep:
    input:
        split_faa = [os.path.join(spl_og_dir, f"{run_title}.contigs_over_{LENGTH_MINIMUM}nt.part_{seqn}.prod.faa") for seqn in seqkit_splits(CPU)]
    params:
        script_dir = config['CENOTE_SCRIPTS'],
        CPU = config["CPU"],
        indir = spl_og_dir,
        repdir = os.path.join(TEMP_DIR, "orig_pyhmmer_rep"),
        repdb = os.path.join(config['C_DBS'], "hmmscan_DBs/virus_replication_clusters3.h3m"),
        evalue = "1e-8"
    output:
        rep_contig_hits = os.path.join(TEMP_DIR, "orig_pyhmmer_rep/contig_hit_count.tsv"),
        rep_report = os.path.join(TEMP_DIR, "orig_pyhmmer_rep/pyhmmer_report_AAs.tsv"),
    shell:
        '''
        python {params.script_dir}/python_modules/pyhmmer_runner.py {params.indir} {params.repdir}\
          {params.repdb} {params.CPU} {params.evalue}
        '''

rule pyhmmer_og_combine:
    input:
        rep_contig_hits = os.path.join(TEMP_DIR, "orig_pyhmmer_rep/contig_hit_count.tsv"),
        rep_report = os.path.join(TEMP_DIR, "orig_pyhmmer_rep/pyhmmer_report_AAs.tsv"),
        vir_contig_hits = os.path.join(TEMP_DIR, "orig_pyhmmer_virion/contig_hit_count.tsv"),
        vir_report = os.path.join(TEMP_DIR, "orig_pyhmmer_virion/pyhmmer_report_AAs.tsv"),
    params:
        script_dir = config['CENOTE_SCRIPTS'],
        virdir = os.path.join(TEMP_DIR, "orig_pyhmmer_virion"),
        repdir = os.path.join(TEMP_DIR, "orig_pyhmmer_rep"),
        hallmin = HALLMARK_MINIMUM,
        htype = config['HALL_TYPE'],
        temdir = TEMP_DIR
    output:
        keep_contigs = os.path.join(TEMP_DIR, "contigs_to_keep.txt"),
        hm_contig_report = os.path.join(TEMP_DIR, "hallmarks_per_orig_contigs.tsv"),
        hm_list_keep = os.path.join(TEMP_DIR, "hallmarks_for_keepcontigs1.txt")
    shell:
        '''
        python {params.script_dir}/python_modules/combine_hallmark_counts.py {params.virdir}\
          {params.repdir} {params.hallmin} {params.htype} {params.temdir}
        '''


rule get_hm_contigs:
    input:
        keep_contigs = os.path.join(TEMP_DIR, "contigs_to_keep.txt"),
        filt_contigs = os.path.join(run_title, \
            f"{run_title}.contigs_over_{LENGTH_MINIMUM}nt.fasta")
    output:
        hm_unp_contigs = os.path.join(TEMP_DIR, "unprocessed_hallmark_contigs.fasta")
    shell:
        '''
        seqkit grep --quiet -f {input.keep_contigs} {input.filt_contigs} > {output.hm_unp_contigs}
        '''

rule terminal_repeats:
    input:
        hm_unp_contigs = os.path.join(TEMP_DIR, "unprocessed_hallmark_contigs.fasta")
    params:
        script_dir = config['CENOTE_SCRIPTS'],
        wrap = config["WRAP"],
        temdir = TEMP_DIR
    output:
        trim_TRs = os.path.join(TEMP_DIR, "trimmed_TRs_hallmark_contigs.fasta"),
        tr_sum = os.path.join(TEMP_DIR, "hallmark_contigs_terminal_repeat_summary.tsv")
    shell:
        '''
        python {params.script_dir}/python_modules/terminal_repeats.py\
          {input.hm_unp_contigs} {params.temdir} {params.wrap}
        '''

rule rotator:
    input:
        trim_TRs = os.path.join(TEMP_DIR, "trimmed_TRs_hallmark_contigs.fasta"),
        tr_sum = os.path.join(TEMP_DIR, "hallmark_contigs_terminal_repeat_summary.tsv")
    params:
        wrap = config["WRAP"],
        temdir = TEMP_DIR
    output:
        orient_contigs = os.path.join(TEMP_DIR, "oriented_hallmark_contigs.fasta")
    shell:
        '''
        if [ {params.wrap} == "True" ] ; then

            if [ ! -d "{params.temdir}/rotation" ]; then
                mkdir {params.temdir}/rotation
            fi

            awk '{{OFS=FS="\t"}}{{ if (NR != 1 && $4 != "NA") {{print $1, $3}} }}'\
            {params.temdir}/hallmark_contigs_terminal_repeat_summary.tsv |\
            while read DTR_CONTIG TRIM_LENGTH ; do
                seqkit grep --quiet -p "${{DTR_CONTIG}}" {params.temdir}/trimmed_TRs_hallmark_contigs.fasta |\
                prodigal -a {params.temdir}/rotation/${{DTR_CONTIG}}.unrotated.faa -i /dev/stdin -c -p meta -q >/dev/null 2>&1
                FWD_GENES=$( grep "^>" {params.temdir}/rotation/${{DTR_CONTIG}}.unrotated.faa | sed 's/ # /	/g' |\
                    awk '{{FS=OFS="\t"}}{{if ($0 ~ "partial=00;start_type" && $4 == 1) {{print $4}} }}' | wc -l )
                REV_GENES=$( grep "^>" {params.temdir}/rotation/${{DTR_CONTIG}}.unrotated.faa | sed 's/ # /	/g' |\
                    awk '{{FS=OFS="\t"}}{{if ($0 ~ "partial=00;start_type" && $4 == -1) {{print $4}} }}' | wc -l )
                if [ $FWD_GENES -ge $REV_GENES ] && [ $FWD_GENES -ge 1 ]; then
                    START_BASE=$( grep "^>" {params.temdir}/rotation/${{DTR_CONTIG}}.unrotated.faa | sed 's/ # /	/g' |\
                        awk '{{FS=OFS="\t"}}{{if ($0 ~ "partial=00;start_type" && $4 == 1) {{print $2, ($3-$2)}} }}' |\
                        sort -rg -k2,2 | head -n1 | cut -f1 )
                    seqkit grep --quiet -p "${{DTR_CONTIG}}" {params.temdir}/trimmed_TRs_hallmark_contigs.fasta |\
                    seqkit restart --quiet -i ${{START_BASE}} > {params.temdir}/rotation/${{DTR_CONTIG}}.rotate.fasta
                elif [ $REV_GENES -ge 1 ]; then
                    seqkit grep --quiet -p "${{DTR_CONTIG}}" {params.temdir}/trimmed_TRs_hallmark_contigs.fasta |\
                    seqkit seq --quiet -t DNA -r -p > {params.temdir}/rotation/${{DTR_CONTIG}}.rc.fna
                    prodigal -a {params.temdir}/rotation/${{DTR_CONTIG}}.rc.faa -i {params.temdir}/rotation/${{DTR_CONTIG}}.rc.fna -p meta -q >/dev/null 2>&1
                    RC_FWD_GENES=$( grep "^>" {params.temdir}/rotation/${{DTR_CONTIG}}.rc.faa | sed 's/ # /	/g' |\
                        awk '{{FS=OFS="\t"}}{{if ($0 ~ "partial=00;start_type" && $4 == 1) {{print $4}} }}' | wc -l )
                    if [ $RC_FWD_GENES -ge 1 ] ; then 
                        START_BASE=$( grep "^>" {params.temdir}/rotation/${{DTR_CONTIG}}.rc.faa | sed 's/ # /	/g' |\
                            awk '{{FS=OFS="\t"}}{{if ($0 ~ "partial=00;start_type" && $4 == 1) {{print $2, ($3-$2)}} }}' |\
                            sort -rg -k2,2 | head -n1 | cut -f1 )
                        cat {params.temdir}/rotation/${{DTR_CONTIG}}.rc.fna |\
                        seqkit restart --quiet -i ${{START_BASE}} > {params.temdir}/rotation/${{DTR_CONTIG}}.rotate.fasta
                    else
                        echo "Can't find suitable ORF to set rotation of ${{DTR_CONTIG}} and will remain unrotated"
                        seqkit grep --quiet -p "${{DTR_CONTIG}}" {params.temdir}/trimmed_TRs_hallmark_contigs.fasta > {params.temdir}/rotation/${{DTR_CONTIG}}.rotate.fasta
                    fi
                else
                    echo "Can't find suitable ORF to set rotation of $nucl_fa and will remain unrotated"
                    seqkit grep --quiet -p "${{DTR_CONTIG}}" {params.temdir}/trimmed_TRs_hallmark_contigs.fasta > {params.temdir}/rotation/${{DTR_CONTIG}}.rotate.fasta
                fi
            done

            awk '{{OFS=FS="\t"}}{{ if (NR != 1 && $4 == "NA") {{print $1}} }}'\
            {params.temdir}/hallmark_contigs_terminal_repeat_summary.tsv > {params.temdir}/hallmark_contigs_no_DTRS.txt

            if [ -s {params.temdir}/hallmark_contigs_no_DTRS.txt ] ; then
                seqkit grep --quiet -f {params.temdir}/hallmark_contigs_no_DTRS.txt\
                {params.temdir}/trimmed_TRs_hallmark_contigs.fasta > {params.temdir}/hallmark_contigs_no_DTRS.fasta
            fi

            ALL_DTRS=$( find {params.temdir}/rotation -type f -name "*.rotate.fasta" )

            if [ -n "$ALL_DTRS" ] ; then
                echo "$ALL_DTRS" | while read SEQ ; do
                    cat $SEQ
                done > {params.temdir}/hallmark_contigs_with_DTRS.rotated.fasta
            fi

            if [ -s {params.temdir}/hallmark_contigs_with_DTRS.rotated.fasta ] ; then
                cat {params.temdir}/hallmark_contigs_with_DTRS.rotated.fasta >> {params.temdir}/oriented_hallmark_contigs.fasta
            fi

            if [ -s {params.temdir}/hallmark_contigs_no_DTRS.fasta ] ; then
                cat {params.temdir}/hallmark_contigs_no_DTRS.fasta >> {params.temdir}/oriented_hallmark_contigs.fasta
            fi


        else
            echo "not wrapping"
            
            cp {params.temdir}/trimmed_TRs_hallmark_contigs.fasta {params.temdir}/oriented_hallmark_contigs.fasta
        fi
        '''

rule mmseqs_blastp_hm_tax:
    input:
        hm_list_keep = os.path.join(TEMP_DIR, "hallmarks_for_keepcontigs1.txt"),
        split_faa = [os.path.join(spl_og_dir, f"{run_title}.contigs_over_{LENGTH_MINIMUM}nt.part_{seqn}.prod.faa") for seqn in seqkit_splits(CPU)]
    params:
        taxdb = os.path.join(config['C_DBS'], "mmseqs_DBs/refseq_virus_prot_taxDB"),
        mm_temp = os.path.join(TEMP_DIR, "hallmark_tax/tmp")
    threads:
        int(CPU)
    output:
        hm_ORFs = os.path.join(TEMP_DIR, "hallmark_tax/orig_hallmark_genes.faa"),
        hm_db = os.path.join(TEMP_DIR, "hallmark_tax/orig_hallmark_genesDB"),
        mm_res = os.path.join(TEMP_DIR, "hallmark_tax/orig_hallmarks_resDB"),
        mmseqs_tab = os.path.join(TEMP_DIR, "hallmark_tax/orig_hallmarks_align.tsv")
    shell:
        '''
        seqkit grep --quiet -f {input.hm_list_keep}\
          {input.split_faa} > {output.hm_ORFs}
        
        mmseqs createdb {output.hm_ORFs} {output.hm_db} -v 1

        mmseqs search {output.hm_db} {params.taxdb} {output.mm_res} {params.mm_temp}\
          -v 1 --start-sens 1 --sens-steps 3 -s 7

        mmseqs convertalis {output.hm_db} {params.taxdb} {output.mm_res} {output.mmseqs_tab}\
          --format-output query,target,pident,alnlen,evalue,theader,taxlineage -v 1
        '''

rule orfcaller_decision:
    input:
        mmseqs_tab = os.path.join(TEMP_DIR, "hallmark_tax/orig_hallmarks_align.tsv"),
        tr_sum = os.path.join(TEMP_DIR, "hallmark_contigs_terminal_repeat_summary.tsv"),
        keep_contigs = os.path.join(TEMP_DIR, "contigs_to_keep.txt")
    params:
        outdir = os.path.join(TEMP_DIR, "hallmark_tax"),
        script_dir = config['CENOTE_SCRIPTS'],
    output:
        prod_seqs = os.path.join(TEMP_DIR, "hallmark_tax/prodigal_seqs1.txt"),
        phan_seqs = os.path.join(TEMP_DIR, "hallmark_tax/phanotate_seqs1.txt")
    shell:
        '''
        python {params.script_dir}/python_modules/orfcaller_decision1.py\
          {input.mmseqs_tab} {input.tr_sum} {params.outdir}
        '''

rule taxed_seqs:
    input:
        prod_seqs = os.path.join(TEMP_DIR, "hallmark_tax/prodigal_seqs1.txt"),
        phan_seqs = os.path.join(TEMP_DIR, "hallmark_tax/phanotate_seqs1.txt")
    output:
        taxed_seqs = os.path.join(TEMP_DIR, "hallmark_tax/taxed_seqs1.txt")
    shell:
        '''
        if [ -s {output.phan_seqs} ] ; then
            cat {output.phan_seqs} >> {output.taxed_seqs}
        fi

        if [ -s {output.prod_seqs} ] ; then
            cat {output.prod_seqs} >> {output.taxed_seqs}
        fi
        if [ -s {output.taxed_seqs} ] ; then
            grep -v -f {output.taxed_seqs} {input.keep_contigs} >> {output.prod_seqs}
        fi
        '''

rule hm_contigs_grep_prod:
    input:
       prod_seqs = os.path.join(TEMP_DIR, "hallmark_tax/prodigal_seqs1.txt"),
       orient_contigs = os.path.join(TEMP_DIR, "oriented_hallmark_contigs.fasta")
    output:
        prod_contigs = os.path.join(TEMP_DIR, "reORF/oriented_hallmark_contigs.prodigal.fasta"),
    shell:
        '''
        seqkit grep --quiet -f {input.prod_seqs} {input.orient_contigs} > {output.prod_contigs}
        '''

rule hm_contigs_split_prod:
    input:
        prod_contigs = os.path.join(TEMP_DIR, "reORF/oriented_hallmark_contigs.prodigal.fasta"),
    params:
        prod_split_dir = os.path.join(TEMP_DIR, "reORF/prod_split"),
        CPU = CPU
    output:
        split_prod_contigs = os.path.join(TEMP_DIR, "reORF/prod_split/oriented_hallmark_contigs.prodigal.part_{seqn}.fasta")
    shell:
        '''
        seqkit split --quiet -j {params.CPU} -p {params.CPU} -O {params.prod_split_dir} {input.prod_contigs}

        touch {output.split_prod_contigs}
        '''

rule hm_contigs_prodigal:
    input:
        split_prod_contigs = os.path.join(TEMP_DIR, "reORF/prod_split/oriented_hallmark_contigs.prodigal.part_{seqn}.fasta")
    output:
        split_prod_faa = os.path.join(TEMP_DIR, "reORF/prod_split/oriented_hallmark_contigs.prodigal.part_{seqn}.prod.faa"),
        split_prod_gff = os.path.join(TEMP_DIR, "reORF/prod_split/oriented_hallmark_contigs.prodigal.part_{seqn}.prod.gff")
    log:
       os.path.join(TEMP_DIR, "reORF/prod_split/oriented_hallmark_contigs.prodigal.part_{seqn}.prod.log")
    shell:
        '''
        if [ -s {input.split_prod_contigs} ] ; then
            prodigal -a {output.split_prod_faa} -i {input.split_prod_contigs}\
              -f gff -o {output.split_prod_gff} -p meta -q > {log} 2>&1
        else
            touch {output.split_prod_faa}
            touch {output.split_prod_gff}
        fi
        '''

rule prod_gcode:
    input:
        prod_seqs = os.path.join(TEMP_DIR, "hallmark_tax/prodigal_seqs1.txt"),
        split_prod_gff = os.path.join(TEMP_DIR, "reORF/prod_split/oriented_hallmark_contigs.prodigal.part_{seqn}.prod.gff")
    output:
        prod_gcodes = os.path.join(TEMP_DIR, "reORF/prod_split/contig_gcodes1.txt")
    shell:
        '''
        if [ -s {input.prod_seqs} ] ; then
            cat ${TEMP_DIR}/hallmark_tax/prodigal_seqs1.txt | while read SEQ ; do
                GCODE=$( grep -A1 "\"${{SEQ}}\"" {input.split_prod_gff} | tail -n1 |\
                  sed 's/.*transl_table=\([0-9]\{{1,2\}}\).*/\1/' )
                
                echo -e "${{SEQ}}\t${{GCODE}}"
            done > {output.prod_gcodes}
        else
            touch {output.prod_gcodes}
        fi
        '''

rule hm_contigs_grep_phan:
    input:
       phan_seqs = os.path.join(TEMP_DIR, "hallmark_tax/phanotate_seqs1.txt"),
       orient_contigs = os.path.join(TEMP_DIR, "oriented_hallmark_contigs.fasta")
    output:
        phan_contigs = os.path.join(TEMP_DIR, "reORF/oriented_hallmark_contigs.phanotate.fasta"),
    shell:
        '''
        seqkit grep --quiet -f {input.phan_seqs} {input.orient_contigs} > {output.phan_contigs}
        '''

rule hm_contigs_split_phan:
    input:
        phan_contigs = os.path.join(TEMP_DIR, "reORF/oriented_hallmark_contigs.phanotate.fasta"),
    params:
        phan_split_dir = os.path.join(TEMP_DIR, "reORF/phan_split"),
        CPU = CPU
    output:
        split_phan_contigs = os.path.join(TEMP_DIR, "reORF/phan_split/oriented_hallmark_contigs.phanotate.part_{seqn}.fasta")
    shell:
        '''
        seqkit split --quiet -j {params.CPU} -p {params.CPU} -O {params.phan_split_dir} {input.phan_contigs}

        touch {output.split_phan_contigs}
        '''

rule hm_contigs_phanotate:
    input:
        split_phan_contigs = os.path.join(TEMP_DIR, "reORF/phan_split/oriented_hallmark_contigs.phanotate.part_{seqn}.fasta")
    output:
        split_phan_tsv = os.path.join(TEMP_DIR, "reORF/phan_split/oriented_hallmark_contigs.phanotate.part_{seqn}.phan_genes.bad_fmt.tsv"),
    log:
       os.path.join(TEMP_DIR, "reORF/phan_split/oriented_hallmark_contigs.prodigal.part_{seqn}.prod.log")
    shell:
        '''
        if [ -s {input.split_phan_contigs} ] ; then
            phanotate.py -f tabular {input.split_phan_contigs} -o {output.split_phan_tsv} > {log} 2>&1
        else
            touch {output.split_phan_tsv}
        fi
        '''

rule phan_to_bed:
    input:
        split_phan_tsv = os.path.join(TEMP_DIR, "reORF/phan_split/oriented_hallmark_contigs.phanotate.part_{seqn}.phan_genes.bad_fmt.tsv"),
    params:
        script_dir = config['CENOTE_SCRIPTS'],
    output:
        split_phan_bed = os.path.join(TEMP_DIR, "reORF/phan_split/oriented_hallmark_contigs.phanotate.part_{seqn}.phan_genes.bed")
    shell:
        '''
        if [ -s {input.split_phan_tsv} ] ; then
            awk '{{OFS=FS="\t"}}{{ if ($1 !~ /^#/) \
              {{ if ($2>$1) {{print $4, ($1-1), $2, $4"_"NR, $5, $3}}\
              else {{print $4, ($2-1), $1, $4"_"NR, $5, $3}} }} }}'\
              {input.split_phan_tsv} > {output.split_phan_bed}
        else
            touch {output.split_phan_bed}
        fi
        '''

rule bed_to_gene:
    input:
        split_phan_contigs = os.path.join(TEMP_DIR, "reORF/phan_split/oriented_hallmark_contigs.phanotate.part_{seqn}.fasta"),
        split_phan_bed = os.path.join(TEMP_DIR, "reORF/phan_split/oriented_hallmark_contigs.phanotate.part_{seqn}.phan_genes.bed")
    output:
        split_phan_nORF = os.path.join(TEMP_DIR, "reORF/phan_split/oriented_hallmark_contigs.phanotate.part_{seqn}.phan_genes.fna"),
    shell:
        '''
        if [ -s {input.split_phan_contigs} ] && [ -s {input.split_phan_bed} ] ; then
            bedtools getfasta -fi {input.split_phan_contigs} -bed {input.split_phan_bed}\
              -fo {output.split_phan_nORF} -s -nameOnly
        else
            touch {output.split_phan_nORF}
        fi
        '''

rule phan_gene_translate:
    input:
        split_phan_nORF = os.path.join(TEMP_DIR, "reORF/phan_split/oriented_hallmark_contigs.phanotate.part_{seqn}.phan_genes.fna"),
    output:
        split_phan_aORF = os.path.join(TEMP_DIR, "reORF/phan_split/oriented_hallmark_contigs.phanotate.part_{seqn}.faa")
    log:
        os.path.join(TEMP_DIR, "reORF/phan_split/oriented_hallmark_contigs.phanotate.part_{seqn}.log")
    shell:
        '''
        if [ -s {input.split_phan_nORF} ] ; then
            seqkit translate --quiet -x -T 11 {input.split_phan_nORF}\
              -o {output.split_phan_aORF} > {log} 2>&1
        else
            touch {output.split_phan_aORF}
        fi
        '''

rule cat_reORF:
    input:
        split_phan_aORF = [os.path.join(TEMP_DIR, f"reORF/phan_split/oriented_hallmark_contigs.phanotate.part_{seqn}.faa") for seqn in seqkit_splits(CPU)],
        split_prod_faa = [os.path.join(TEMP_DIR, f"reORF/prod_split/oriented_hallmark_contigs.prodigal.part_{seqn}.prod.faa") for seqn in seqkit_splits(CPU)]
    output:
        all_reORF = os.path.join(TEMP_DIR, "reORF/reORFcalled_all.faa")
    shell:
        '''
        cat {input.split_phan_aORF} {input.split_prod_faa} | sed 's/(.*//g' > {output.all_reORF}
        '''

rule split_reORF:
    input:
        all_reORF = os.path.join(TEMP_DIR, "reORF/reORFcalled_all.faa")
    params:
        reORF_split_dir = os.path.join(TEMP_DIR, "reORF_pyhmmer1_split"),
        CPU = CPU
    output:
        split_reORF = os.path.join(TEMP_DIR, "reORF_pyhmmer1_split/reORFcalled_all.part_{seqn}.faa")
    shell:
        '''
        seqkit split --quiet -j {params.CPU} -p {params.CPU} -O {params.reORF_split_dir} {input.all_reORF}

        touch {output.split_reORF}
        '''

use rule pyhmmer_prodigal_og_vir as pyhmmer_prodigal_reORF_vir with:
    input:
        split_faa = [os.path.join(TEMP_DIR, f"reORF_pyhmmer1_split/reORFcalled_all.part_{seqn}.faa") for seqn in seqkit_splits(CPU)]
    params:
        indir = os.path.join(TEMP_DIR, "reORF_pyhmmer1_split"),
        virdir = os.path.join(TEMP_DIR, "virion_reORF_pyhmmer"),
        evalue = "1e-6",
        script_dir = config['CENOTE_SCRIPTS'],
        CPU = config["CPU"],
        viriondb = os.path.join(config['C_DBS'], "hmmscan_DBs/virus_specific_baits_plus_missed6a.h3m"),
    output:
        vir_contig_hits = os.path.join(TEMP_DIR, "virion_reORF_pyhmmer/contig_hit_count.tsv"),
        vir_report = os.path.join(TEMP_DIR, "virion_reORF_pyhmmer/pyhmmer_report_AAs.tsv")

use rule pyhmmer_prodigal_og_rep as pyhmmer_prodigal_reORF_rep with:
    input:
        split_faa = [os.path.join(TEMP_DIR, f"reORF_pyhmmer1_split/reORFcalled_all.part_{seqn}.faa") for seqn in seqkit_splits(CPU)]
    params:
        indir = os.path.join(TEMP_DIR, "reORF_pyhmmer1_split"),
        repdir = os.path.join(TEMP_DIR, "rep_reORF_pyhmmer"),
        evalue = "1e-6",
        script_dir = config['CENOTE_SCRIPTS'],
        CPU = config["CPU"],
        repdb = os.path.join(config['C_DBS'], "hmmscan_DBs/virus_replication_clusters3.h3m"),
    output:
        rep_contig_hits = os.path.join(TEMP_DIR, "rep_reORF_pyhmmer/contig_hit_count.tsv"),
        rep_report = os.path.join(TEMP_DIR, "rep_reORF_pyhmmer/pyhmmer_report_AAs.tsv"),

rule hits_pyhmmer_repvir:
    input:
        os.path.join(TEMP_DIR, "virion_reORF_pyhmmer/pyhmmer_report_AAs.tsv"),
        os.path.join(TEMP_DIR, "rep_reORF_pyhmmer/pyhmmer_report_AAs.tsv"),
    output:
        rd_hits = os.path.join(TEMP_DIR, "virion_reORF_pyhmmer/hit_this_round1.txt")
    shell:
        '''
        awk FNR!=1 {input} | cut -f1 > {output.rd_hits}
        '''

rule grep_nohit_repvir:
    input:
        rd_hits = os.path.join(TEMP_DIR, "virion_reORF_pyhmmer/hit_this_round1.txt"),
        split_reORF = os.path.join(TEMP_DIR, "reORF_pyhmmer1_split/reORFcalled_all.part_{seqn}.faa")
    params:
        CPU = CPU
    output:
        no1_AAs = os.path.join(TEMP_DIR, "reORF_pyhmmer2_split/reORFcalled_all.part_{seqn}.no1.faa")
    shell:
        '''
        if [ -s {input.rd_hits} ] ; then
            seqkit grep --quiet -j {params.CPU} -v -f {input.rd_hits} {input.split_reORF} > {output.no1_AAs}
        else
            cp {input.split_reORF} {output.no1_AAs}
        fi
        '''

use rule pyhmmer_prodigal_og_vir as pyhmmer_prodigal_reORF_com with:
    input:
        split_faa = [os.path.join(TEMP_DIR, f"reORF_pyhmmer2_split/reORFcalled_all.part_{seqn}.no1.faa") for seqn in seqkit_splits(CPU)]
    params:
        indir = os.path.join(TEMP_DIR, "reORF_pyhmmer2_split"),
        virdir = os.path.join(TEMP_DIR, "comm_reORF_pyhmmer"),
        evalue = "1e-4",
        script_dir = config['CENOTE_SCRIPTS'],
        CPU = config["CPU"],
        viriondb = os.path.join(config['C_DBS'], "hmmscan_DBs/useful_hmms_baits_and_not2a.h3m"),
    output:
        vir_contig_hits = os.path.join(TEMP_DIR, "comm_reORF_pyhmmer/contig_hit_count.tsv"),
        vir_report = os.path.join(TEMP_DIR, "comm_reORF_pyhmmer/pyhmmer_report_AAs.tsv")

use rule hits_pyhmmer_repvir as hits_pyhmmer_com with:
    input:
        os.path.join(TEMP_DIR, "comm_reORF_pyhmmer/pyhmmer_report_AAs.tsv")
    output:
        rd_hits = os.path.join(TEMP_DIR, "comm_reORF_pyhmmer/hit_this_round1.txt")


rule grep_nohit_com:
    input:
        rd_hits = os.path.join(TEMP_DIR, "comm_reORF_pyhmmer/hit_this_round1.txt"),
        no1_AAs = [os.path.join(TEMP_DIR, f"reORF_pyhmmer2_split/reORFcalled_all.part_{seqn}.no1.faa") for seqn in seqkit_splits(CPU)]
    params:
        CPU = CPU
    output:
        no2_AAs = os.path.join(TEMP_DIR, "reORF_mmseqs_combined/all_AA_seqs.no2.faa")
    shell:
        '''
        if [ -s {input.rd_hits} ] ; then
            seqkit grep --quiet -j {params.CPU} -v -f {input.rd_hits} {input.no1_AAs} > {output.no2_AAs}
        else
            cat {input.no1_AAs} > {output.no2_AAs}
        fi
        '''


rule mmseqs_cdd:
    input:
        no2_AAs = os.path.join(TEMP_DIR, "reORF_mmseqs_combined/all_AA_seqs.no2.faa")
    params:
        cdd_db = os.path.join(config['C_DBS'], "mmseqs_DBs/CDD"),
        mm_temp = os.path.join(TEMP_DIR, "reORF_mmseqs_combined/tmp")
    threads:
        int(CPU)
    output:
        rem_db = os.path.join(TEMP_DIR, "reORF_mmseqs_combined/all_AA_seqs.no2DB"),
        cdd_res = os.path.join(TEMP_DIR, "reORF_mmseqs_combined/all_AA_seqs.no2_vs_CDD_resDB"),
        mmseqs_tab = os.path.join(TEMP_DIR, "reORF_mmseqs_combined/no2_seqs_CDD.tsv")
    shell:
        '''
        mmseqs createdb {input.no2_AAs} {output.rem_db} -v 1

        touch {output.cdd_res}
        
        mmseqs search {output.rem_db} {params.cdd_db} {output.cdd_res} {params.mm_temp} -s 4 -v 1

        mmseqs convertalis {output.rem_db} {params.cdd_db} {output.cdd_res} {output.mmseqs_tab}\
          --format-output query,target,pident,alnlen,evalue,bits -v 1
        '''

rule mmseqs_besthit:
    input:
        mmseqs_tab = os.path.join(TEMP_DIR, "reORF_mmseqs_combined/no2_seqs_CDD.tsv")
    params:
        cdd_annot = os.path.join(config['C_DBS'], "mmseqs_DBs/cddid_all.tbl"),
        wdir = os.path.join(TEMP_DIR, "reORF_mmseqs_combined"),
        script_dir = config['CENOTE_SCRIPTS'],
    output:
        mm_best = os.path.join(TEMP_DIR, "reORF_mmseqs_combined/summary_no2_AAs_vs_CDD.besthit.tsv")
    shell:
        '''
        python {params.script_dir}/python_modules/parse_mmseqs_cdd_results1.py\
          {input.mmseqs_tab} {params.cdd_annot} {params.wdir}
        '''

rule assess_genes:
    input:
        tr_sum = os.path.join(TEMP_DIR, "hallmark_contigs_terminal_repeat_summary.tsv"),
        split_phan_bed = [os.path.join(TEMP_DIR, f"reORF/phan_split/oriented_hallmark_contigs.phanotate.part_{seqn}.phan_genes.bed") for seqn in seqkit_splits(CPU)],
        split_prod_gff = [os.path.join(TEMP_DIR, f"reORF/prod_split/oriented_hallmark_contigs.prodigal.part_{seqn}.prod.gff") for seqn in seqkit_splits(CPU)],
        vir_report = os.path.join(TEMP_DIR, "virion_reORF_pyhmmer/pyhmmer_report_AAs.tsv"),
        com_report = os.path.join(TEMP_DIR, "comm_reORF_pyhmmer/pyhmmer_report_AAs.tsv"),
        rep_report = os.path.join(TEMP_DIR, "rep_reORF_pyhmmer/pyhmmer_report_AAs.tsv"),
        mm_best = os.path.join(TEMP_DIR, "reORF_mmseqs_combined/summary_no2_AAs_vs_CDD.besthit.tsv"),
    params:
        htype = config['HALL_TYPE'],
        bed_dir = os.path.join(TEMP_DIR, "reORF/phan_split"),
        gff_dir = os.path.join(TEMP_DIR, "reORF/prod_split"),
        vir_dom_list = os.path.join(config['C_DBS'], "viral_cdds_and_pfams_191028.txt"),
        script_dir = config['CENOTE_SCRIPTS'],
        outdir = os.path.join(TEMP_DIR, "assess_prune")
    output:
        assess_tab = os.path.join(TEMP_DIR, "assess_prune/contig_gene_annotation_summary.tsv"),
        hm_bed = os.path.join(TEMP_DIR, "assess_prune/contig_gene_annotation_summary.hallmarks.bed"),
        indiv_dir = directory(os.path.join(TEMP_DIR, "assess_prune/indiv_seqs"))
    shell:
        '''
        python {params.script_dir}/python_modules/assess_virus_genes1.py {input.tr_sum}\
          {params.bed_dir} {params.gff_dir} {input.vir_report} {input.com_report} {input.rep_report}\
          {input.mm_best} {params.vir_dom_list} {params.outdir} {params.htype}
        '''


